/* Question 2: The dining savages */

/* Process SAVAGE models the behavior of a savage where:
 * (i) A savage is always eager to get a serving from the pot. */

SAVAGE = ( get_serving -> SAVAGE ).

/* Process SAVAGES models a finite number of savages */

range Savages = 1..4

||SAVAGES = ( forall[i: Savages] savage[i]:SAVAGE ).

/* Process COOK models the behavior of a cook where:
 * (i) A cook is always trying to fill the pot. */

COOK = ( fill_pot -> COOK ).

/* Process POT models the behavior of a pot where:
 * (i) The pot has an associated state indicating the current number
 *     of stewed missionaries.
 * (ii) Initially it is assumed that the pot is empty and that
 *      the maximum number of stewed missionaries is M.
 * (iii) Action take_portion models a portion being taken from
 *       the pot whenever possible.
 * (iv) Action fill models the pot being filled. */

const M = 3
range Portions = 0..M

POT = POT[0],
POT[p: Portions] = (
    when ( p > 0 ) take_portion -> POT[p-1]
    | fill -> POT[M] ).

/* Dining savages */

||DINING_SAVAGES = ( SAVAGES || pot:POT || cook:COOK )
/{ 
    savage[s: Savages].get_serving/pot.take_portion,
    cook.fill_pot/pot.fill
}.
